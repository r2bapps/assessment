plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.r2b.apps.xuser"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "api"
    productFlavors {
        marvel {
            dimension "api"
            applicationIdSuffix ".marvel"
            versionNameSuffix "-marvel"
            buildConfigField "String" , "MARVEL_PRIVATE_KEY" , "\"${project.properties.get('MARVEL_PRIVATE_KEY')}\""
            buildConfigField "String" , "MARVEL_PUBLIC_KEY" , "\"${project.properties.get('MARVEL_PUBLIC_KEY')}\""
        }
        randomuser {
            dimension "api"
            applicationIdSuffix ".randomuser"
            versionNameSuffix "-randomuser"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation project(':data')
    implementation project(':data-marvel')
    implementation project(':data-randomuser')
    implementation project(':domain')
    implementation project(':lib:logger')
    implementation project(':lib:tracker')
    implementation project(':lib:image-loader')

    // TODO: Extract grouped dependencies

    // HILT
    kapt "androidx.hilt:hilt-compiler:$rootProject.hilt_jetpack_version"
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-compiler:$rootProject.hilt_version"

    // NAVIGATION
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation_jetpack_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigation_jetpack_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$rootProject.navigation_jetpack_version"

    // RETROFIT + MOSHI + OKHTTP
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"
    implementation "com.squareup.moshi:moshi:$rootProject.moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$rootProject.moshi_version"

    // ADAPTER DELEGATE
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl:$rootProject.hannesdorfmann_version"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$rootProject.hannesdorfmann_version"

    // ANDROID
    implementation "androidx.core:core-ktx:$rootProject.core_ktx_version"
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "com.google.android.material:material:$rootProject.material_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayout_version"

    // TESTING
    testImplementation "junit:junit:$rootProject.junit_version"
    testImplementation "io.mockk:mockk:$rootProject.mockk_version"
    testImplementation "io.mockk:mockk-agent-jvm:$rootProject.mockk_version"
    testImplementation "app.cash.turbine:turbine:$rootProject.turbine_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.coroutines_version"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.junit_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.expresso_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$rootProject.navigation_jetpack_version"
}

kapt {
    correctErrorTypes true
}